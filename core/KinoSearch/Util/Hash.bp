parcel KinoSearch cnick Kino;

/** 
 * Hashtable.
 *
 * Private hashtable module for KinoSearch's internal use.  The keys are
 * CharBufs; the values may belong to any subclass of Obj. The hashing function
 * is the one used by Perl 5.8.8: Bob Jenkin's "one-at-a-time" algorithm.
 * Collisions are resolved using a linked list.
 */ 

class KinoSearch::Util::Hash extends KinoSearch::Obj {

    void          *entries;
    u32_t          capacity;
    u32_t          size;
    u32_t          threshold;    /* rehashing trigger point */
    i32_t          iter_tick;    /* used when iterating */

    static SharedKeyMasterHash *keymaster;

    /** 
     * @param capacity The number of elements that the hash will be asked to
     * hold initially.
     */
    static incremented Hash*
    new(u32_t capacity = 0);

    static Hash*
    init(Hash *self, u32_t capacity = 0);

    /** Empty the hash of all key-value pairs.
     */
    void
    Clear(Hash *self);

    /** Store a key-value pair, using a copy of the CharBuf as a key.
     */
    void
    Store(Hash *self, const CharBuf *key, decremented Obj *value);

    void
    Store_Str(Hash *self, const char *str, size_t len, 
              decremented Obj *value);

    /** Fetch the value associated with a given key.  
     * 
     * @return the value, or NULL if the key is not present.
     */
    Obj*
    Fetch(Hash *self, const CharBuf *key);

    Obj*
    Fetch_Str(Hash *self, const char *key, size_t key_len);

    /** Attempt to delete a key-value pair from the hash.  
     *
     * @return If the key exists * and the deletion is successful, return the
     * value; otherwise NULL.
     */
    incremented Obj*
    Delete(Hash *self, const CharBuf *key);

    incremented Obj*
    Delete_Str(Hash *self, const char *key, size_t key_ley);

    /** Prepare to iterate over all the key-value pairs in the hash.
     *
     * @return the number of pairs which will be iterated over.
     */
    u32_t 
    Iter_Init(Hash *self);

    /** Retrieve the next key-value pair from the hash, setting the supplied
     * pointers to point at them.  
     * 
     * @return true while iterating, false when the iterator has been
     * exhausted.
     */
    bool_t
    Iter_Next(Hash *self, CharBuf **key, Obj **value);

    /** Search for a key which Equals the key supplied, and return the key
     * rather than its value.
     */
    CharBuf*
    Find_Key(Hash *self, const CharBuf *key, i32_t hash_code);

    /** Return an VArray of pointers to the hash's keys.
     */
    incremented VArray*
    Keys(Hash *self);

    /** Return an VArray of pointers to the hash's values.
     */
    incremented VArray*
    Values(Hash *self);

    /** Store a list of key value pairs.  The keys must be char*, and the
     * values must be Obj*.
     * 
     *   Hash_marshall(hash, 3, 
     *       "foo", foo,
     *       "bar", bar,
     *       "baz", baz,
     *   );
     */
    static void
    marshall(Hash *self, u32_t num_pairs, ...);

    /** Lame debugging dump -- just good enough for a quick hack.
     */
    incremented CharBuf*
    Dumper(Hash *self);

    /** Create a key to be stored within the hash entry.  Implementations must
     * supply an object which produces the same Hash_Code() value and tests
     * true for Equals().
     */
    public incremented SharedHashKey*
    Make_Key(Hash *self, const CharBuf *key, i32_t hash_code);

    u32_t 
    Get_Capacity(Hash *self);

    /** Accessor for Hash's "size" member.
     * 
     * @return the number of key-value pairs.
     */
    public u32_t 
    Get_Size(Hash *self);

    public bool_t
    Equals(Hash *self, Obj *other);

    public incremented Hash*
    Dump(Hash *self);

    public incremented Obj*
    Load(Hash *self, Obj *dump);

    public void
    Serialize(Hash *self, OutStream *outstream);

    public incremented Hash*
    Deserialize(Hash *self, InStream *instream);

    void
    Destroy(Hash *self);
}

class KinoSearch::Util::Hash::SharedKeyMasterHash 
    extends KinoSearch::Util::Hash {

    static incremented SharedKeyMasterHash*
    new(u32_t request);

    static SharedKeyMasterHash*
    init(SharedKeyMasterHash *self, u32_t request);

    public incremented SharedHashKey*
    Make_Key(SharedKeyMasterHash *self, const CharBuf *key, i32_t hash_code);
}

class KinoSearch::Util::Hash::SharedHashKey extends KinoSearch::Util::CharBuf {

    static incremented SharedHashKey*
    new(const CharBuf *source, i32_t ash_value);

    static SharedHashKey*
    init(SharedHashKey *self, const CharBuf *source, i32_t hash_code);

    u32_t
    Dec_RefCount(SharedHashKey *self);
}

/* Copyright 2006-2009 Marvin Humphrey
 *
 * This program is free software; you can redistribute it and/or modify
 * under the same terms as Perl itself.
 */

